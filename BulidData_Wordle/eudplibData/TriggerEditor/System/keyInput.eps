import System.phonemes as sp;
import System.word as sw;
import System.keyboard as skb;
import Player.scoreBoard as psb;
import variables as v;

const s = StringBuffer(1024);

var   question          = 0; //문제의 번호
const keywordLength     = 5; //단어의 길이
const keywordNumber     = 6*keywordLength;//정답의 기회
const keywordUse        = PVariable();
const keywordUpdate     = PVariable();
const keywordIndex      = PVariable();
const keywordLine       = PVariable();
const keywordArray      = EUDArray(8*keywordNumber);
const keywordColor      = EUDArray(8*keywordNumber);
const keywordDelay      = PVariable();
const keywordInput      = PVariable();
const keyboardColor     = EUDArray(8*26); //각 초성별 색상


function stopKeyword(cp){
    SetDeaths(cp, SetTo, 0, "A Check");
    keywordUse[cp] = 1;
}

// 단어리스트에 있는 단어인지 검색하기
function checkKeyword(cp){
    const cpk = cp*keywordNumber;
    const wordLine = keywordLine[cp]*keywordLength;
    const firstLetter = sp.phonemesOrder[keywordArray[cpk+wordLine+0]-1];
    // 첫번째 단어가 어떤 초성인지 분석하기
    var firstIndex = 0;
    var searchRange = 0;
    for (var i=0; i<14; i++){
        searchRange = sw.wordNumber[i];
        if(firstLetter == i){break;}
        else{firstIndex += sw.wordNumber[i];}
    }
    // firstIndex를 기준으로 값 찾기
    var wordIndex = 0;
    for (var i=0; i<searchRange; i++){
        wordIndex = firstIndex+i;
        if(wordIndex == sw.wordMAX-2){break;}
        var check = true;
        const word = sw.wordList[wordIndex];
        for (var j=0; j<keywordLength; j++){
            const value = sp.phonemesValue[keywordArray[cpk+wordLine+j]-1];
            if(!MemoryXEPD(EPD(word)+j, Exactly, value, 0xFFFFFF)){
                check = false;
                break;
            }
        }
        if(check){
            // 정답과 일치
            if(question == wordIndex){return 2;}
            // 단어리스트에 존재
            else{return 1;}
        }
    }
    return 0;
}

function nextLine(cp){
    const wordNum   = EUDArray(26);
    const wordIndex = EUDArray(keywordLength);
    const myWord    = EUDArray(keywordLength);
    var   myWordNum = keywordLength;
    for (var i=0; i<26; i++){
        wordNum[i] = 0;
        if(i<keywordLength){
            wordIndex[i] = 0;
            myWord[i] = 1;
        }
    }
    const answer = sw.wordList[question];
    // 정답 글자 분해
    for (var i=0; i<keywordLength; i++){
        for (var j=0; j<26; j++){
            if(MemoryXEPD(EPD(answer)+i, Exactly, sp.phonemesValue[j], 0xFFFFFF)){
                wordNum[j] += 1; //wordNum[0~25]
                wordIndex[i] = j+1; //wordIndex[0~5] = 1~26
                break;
            }
        }
    }
    const cpk = cp*keywordNumber;
    const cpb = cp*26;
    const wordLine = keywordLine[cp]*keywordLength;
    // 내 글자 해석
    for (var i=0; i<keywordLength; i++){
        const myIndex = keywordArray[cpk+wordLine+i]-1; //1~26
        // 글자가 일치하는 경우
        if(myWord[i] > 0 && wordIndex[i] > 0 && wordIndex[i] == myIndex+1){
            myWordNum -= 1;
            wordNum[wordIndex[i]-1] -= 1;
            wordIndex[i] = 0; myWord[i] = 0;
            keywordColor[cpk+wordLine+i] = 3;
            skb.keyboardColor[cpb+myIndex] = 3;
        }
    }
    for (var i=0; i<keywordLength; i++){
        if(myWordNum == 0) break;
        const myIndex = keywordArray[cpk+wordLine+i]-1; //1~26
        // 위치가 일치하지 않지만, 다른 위치에 같은 글자가 있는 경우
        if(myWord[i] > 0 && wordNum[myIndex] > 0){
            wordNum[myIndex] -= 1;
            myWord[i] = 0; myWordNum -= 1;
            keywordColor[cpk+wordLine+i] = 2;
            if(MemoryEPD(EPD(skb.keyboardColor)+cpb+myIndex, AtMost, 1)){
                skb.keyboardColor[cpb+myIndex] = 2;
            }
        }
    }
    for (var i=0; i<keywordLength; i++){
        if(myWordNum == 0) break;
        const myIndex = keywordArray[cpk+wordLine+i]-1; //1~26
        // 아무것도 해당하지 않는 경우
         if(myWord[i] > 0){
            myWord[i] = 0; myWordNum -= 1;
            keywordColor[cpk+wordLine+i] = 1;
            if(MemoryEPD(EPD(skb.keyboardColor)+cpb+myIndex, Exactly, 0)){
                skb.keyboardColor[cpb+myIndex] = 1;
            }
        }
    }
    keywordLine[cp] ++;
    if(keywordLine[cp] == 6){
        stopKeyword(cp);
    }
    keywordIndex[cp] = 0;
    keywordUpdate[cp] = 1;
    skb.keyboardUpdate[cp] = 1;
}

function inputAnswer(cp){
    if(keywordInput[cp] == 1){
        keywordInput[cp] = 0;
        const result = checkKeyword(cp);
        // 정답인 경우
        if(result == 2){
            stopKeyword(cp);
            psb.calculateScore(cp, keywordLine[cp]);
            nextLine(cp);
        }
        // 정답은 아니지만 DB에 존재하는 단어인 경우
        else if(result == 1){
            nextLine(cp);
        }
        // DB에도 존재하지 않는 단어인 경우
        else{
            eprintf("\x08！\x04없는 단어입니다.");
        }
    }
}

function inputKeyword(cp){
    const cpk = cp*keywordNumber;
    for (var i=0; i<sp.phonemesNumber; i++){
        if(Deaths(cp, Exactly, sp.phonemesKeys[i], sp.phonemesName[i])){
            if (keywordIndex[cp] < keywordLength){
                const lines = keywordLine[cp]*keywordLength;
                keywordArray[cpk+lines+keywordIndex[cp]] = i+1;
                keywordIndex[cp] ++;
                keywordUpdate[cp] = 1;
                if(keywordIndex[cp] == keywordLength){keywordInput[cp] = 12;}
                PlayWAV("staredit\\wav\\elec-comp-keyboard-key1.wav");
            }
        }
    }
}

function deleteKeyword(cp){
    const cpk = cp*keywordNumber;
    if(Deaths(cp, Exactly, 2, "A Key4")){
        if(keywordIndex[cp] > 0 && keywordDelay[cp] == 0){
            const lines = keywordLine[cp]*keywordLength;
            keywordInput[cp] = 0;
            keywordDelay[cp] = 6;
            keywordIndex[cp] --;
            keywordUpdate[cp] = 1;
            keywordArray[cpk+lines+keywordIndex[cp]] = 0;
            PlayWAV("staredit\\wav\\elec-comp-keyboard-key2.wav");
        }
    }
    else if(Deaths(cp, Exactly, 0, "A Key4") && keywordDelay[cp] > 0){
        keywordDelay[cp] = 0;
    }
}

function printKeyword(cp){
    if(keywordUpdate[cp] > 0){
        keywordUpdate[cp] = 0;
        s.insert(0);
        const cpk = cp*keywordNumber;
        const wordLine = keywordLine[cp];
        // 이미 입력된 단어 출력하기
        for (var loop=0; loop<wordLine; loop++){
            const lines = loop*keywordLength;
            s.appendf("\x13  ");
            for (var i=0; i<keywordLength; i++){
                s.appendf("{:s}{:s}  ", v.colorSet[keywordColor[cpk+lines+i]], sp.phonemesList[keywordArray[cpk+lines+i]-1]);
            }
            s.appendf("\n");
        }
        if(keywordUse[cp] == 0){
            // 입력중인 단어
            s.appendf("\x13\x08▶  ");
            for (var i=0; i<keywordLength; i++){
                const lines = wordLine*keywordLength;
                if(MemoryEPD(EPD(keywordArray)+cpk+lines+i, Exactly, 0)){
                    s.appendf("\x1E＿  ");
                }
                else{
                    s.appendf("\x04{:s}  ", sp.phonemesList[keywordArray[cpk+lines+i]-1]);
                }
            }
            s.appendf("　");
        }
        if(wordLine < keywordLength){
            for (var i=0; i<keywordLength-wordLine; i++){
                s.appendf("\n"); //\n\x13\x1E ＿ ＿ ＿ ＿ ＿ 
            }
        }
        s.appendf("\n");
    }
    s.DisplayAt(0);
}

function resetKeywordData(){
    foreach(cp : EUDLoopPlayer(None, Force1)){
        const cpk = cp*keywordNumber;
        const cpb = cp*26;
        for (var i=0; i<keywordNumber; i++){
            keywordColor[cpk+i] = 0;
            keywordArray[cpk+i] = 0;
        }
        for (var i=0; i<26; i++){
            skb.keyboardColor[cpb+i] = 0;
        }
        keywordIndex[cp] = 0;
        keywordLine[cp] = 0;
        keywordUse[cp] = 0;
    }
}

function keywordTimer(cp){
    if(keywordDelay[cp]>0){keywordDelay[cp]--;}
    if(keywordInput[cp]>1){keywordInput[cp]--;}
}

function keyInputSystem(cp){
    if(keywordUse[cp] == 0){
        inputKeyword(cp);
        deleteKeyword(cp);
    }
    printKeyword(cp);
    skb.printKeyboard(cp);
    inputAnswer(cp);
    if(Deaths(cp, Exactly, 1, "A Key4")){
        if(skb.keyboardUse[cp] == 0){
            skb.keyboardUse[cp] = 1;
            skb.removeKeyboard(cp);
        }
        else{
            skb.keyboardUse[cp] = 0;
            skb.keyboardUpdate[cp] = 1;
        }
        PlayWAV("staredit\\wav\\switch3.wav");
    }
    //eprintf("{:s}", sw.wordList[question]);
}